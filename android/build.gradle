apply plugin: 'com.android.library'

// Android dependencies
def DEFAULT_COMPILE_SDK_VERSION                     = 28
def DEFAULT_TARGET_SDK_VERSION                      = 28
def DEFAULT_APP_COMPAT_VERSION                      = "28.0.0"

// Plugin dependencies
def DEFAULT_GOOGLE_PLAY_SERVICES_LOCATION_VERSION   = "17.0.0"
def DEFAULT_OK_HTTP_VERSION                         = "3.12.1"
def DEFAULT_ANDROID_PERMISSIONS_VERSION             = "0.1.8"
def DEFAULT_EVENTBUS_VERSION                        = "3.0.0"


def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
}

repositories{
    maven {
        url './libs'
    }
}

dependencies {
    def appCompatVersion = safeExtGet('appCompatVersion', safeExtGet('supportLibVersion', DEFAULT_APP_COMPAT_VERSION))

    def appCompatMajorVersion = appCompatVersion.split('\\.')[0] as int
    def appCompatLibName =  (appCompatMajorVersion < 20) ? "androidx.appcompat:appcompat" : "com.android.support:appcompat-v7"
 
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "$appCompatLibName:$appCompatVersion"
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
}
